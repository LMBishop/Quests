plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
}

processResources {
    duplicatesStrategy = 'include'
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand('version': project.version)
    }
}

repositories {
    // Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // ASkyBlock, BentoBox
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    // uSkyBlock
    maven { url = 'https://raw.github.com/rlf/uSkyBlock/mvn-repo/' }
    // Citizens
    maven { url = 'https://repo.citizensnpcs.co/' }
    // MythicMobs
    maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
    // PlaceholderAPI
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // EssentialsX
    maven { url = 'https://repo.essentialsx.net/releases/' }
    // JitPack (IridiumSkyblock, ShopGUI+)
    maven { url = 'https://jitpack.io' }
    // CoreProtect
    maven { url = 'https://maven.playpro.com/' }
    // bStats
    maven { url = uri('https://repo.codemc.org/repository/maven-public') }
    mavenCentral()
}

dependencies {
    compileOnly project(':common')
    // Spigot
    compileOnly ('org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT') {
        exclude group: 'net.md-5', module: 'bungeecord-chat'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    // ASkyBlock
    compileOnly ('com.wasteofplastic:askyblock:3.0.9.4') {
        exclude group: 'net.milkbowl.vault', module: 'VaultAPI'
    }
    // BentoBox (bSkyBlock)
    compileOnly ('world.bentobox:bentobox:1.15.5-SNAPSHOT') {
        exclude group: 'org.eclipse.jdt', module: 'org.eclipse.jdt.annotation'
        exclude group: 'org.mongodb', module: 'mongodb-driver'
    }
    // IridiumSkyblock
    // TODO fix whenever repo is up
    //    compileOnly 'com.github.Iridium-Development:IridiumSkyblock:master-SNAPSHOT'
    // download  from spigot and add to /libs/ yourself
    // uSkyBlock
    compileOnly 'com.github.rlf:uSkyBlock-API:2.6.4'
    // MythicMobs
    compileOnly 'io.lumine.xikage:MythicMobs:4.11.0-BETA'
    // Citizens
    compileOnly ('net.citizensnpcs:citizens:2.0.27-SNAPSHOT') {
        exclude group: 'net.citizensnpcs', module: 'citizens-main'
        exclude group: 'net.citizensnpcs', module: 'citizens-v1_8_R3'
        exclude group: 'net.citizensnpcs', module: 'citizens-v1_12_R1'
        exclude group: 'net.citizensnpcs', module: 'citizens-v1_13_R2'
        exclude group: 'net.citizensnpcs', module: 'citizens-v1_14_R1'
        exclude group: 'net.citizensnpcs', module: 'citizens-v1_15_R1'
        exclude group: 'net.citizensnpcs', module: 'citizens-v1_16_R3'
    }
    // PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.10.9'
    // EssentialsX
    compileOnly "net.ess3:EssentialsX:2.18.2"
    // ShopGUI+
    compileOnly 'com.github.brcdev-minecraft:shopgui-api:master-SNAPSHOT'
    // CoreProtect
    compileOnly ('net.coreprotect:coreprotect:2.18.2') {
        exclude group: 'com.sk89q.worldedit', module: 'worldedit-bukkit'
    }
    // JetBrains Annotations
    compileOnly 'org.jetbrains:annotations:16.0.2'
    // bStats
    implementation 'org.bstats:bstats-bukkit-lite:1.8'
    // HikariCP
    implementation 'com.zaxxer:HikariCP:4.0.3'
    // slf4j
    implementation 'org.slf4j:slf4j-simple:1.7.30'

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

tasks.build {
    dependsOn(tasks.shadowJar)
}

shadowJar {
    relocate 'org.bstats', 'com.leonardobishop.quests.libs.bstats'
    relocate 'com.zaxxer.hikari', 'com.leonardobishop.quests.libs.hikaricp'
    relocate 'org.slf4j', 'com.leonardobishop.quests.libs.slf4j'
    archiveClassifier.set('')
}